// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
 class Keyboard {

    /** Initializes the keyboard. */
    function void init() {
        return;
    } 

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
        var char key;
        let key = Memory.peek(24576);

        if (key = 0) {
            return 0 ;
        }
        return key;
    }

    /**								
     * Waits until a key is pressed on the keyboard and released,
     * then echoes the key to the screen, and returns the character 
     * of the pressed key.
     */
    function char readChar() {
        var char key, c;

        let key = 0;
        while (key = 0) {
            let key = Keyboard.keyPressed();
        }
        let c = key;
        while (~(key = 0)) {
            let key = Keyboard.keyPressed();
        }
        do Output.printChar(c);

        return c;
    }

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its value. Also handles user backspaces.
     */
    function String readLine(String message) {
        /*
        * Recognizes all ASCII characters, as well as the following keys:
        * new line = 128 = String.newline()
        * backspace = 129 = String.backspace()
        * left arrow = 130
        * up arrow = 131
        * right arrow = 132
        * down arrow = 133
        * home = 134
        * End = 135
        * page up = 136
        * page down = 137
        * insert = 138
        * delete = 139
        * ESC = 140
        * F1 - F12 = 141 - 152
        */
        var char key, c;
        var int length;
        var String str;

        let str = String.new(200);
        do Output.printString(message);

        let key = 0;
        let c = 0;
        let length = 0;
        while (~(c = 128)) {
            while (key = 0) {
                let key = Keyboard.keyPressed();
            }
            let c = key;
            while (~(key = 0)) {
                let key = Keyboard.keyPressed();
            }
            // do Output.printChar(key);
            if (( c > 31 ) & ( c < 127)) {
                let str = str.appendChar(c);
                let length = length + 1;
                do Output.printChar(c);
            }
            if (c = 128) {
                do Output.println();
            }
            if (c = 129) {
                if (length > 0) {
                    do Output.backSpace();
                    do str.eraseLastChar();
                    let length = length - 1;
                }
            }
        }
        return str;
    }   

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its integer value (until the first non-digit character in the
     * entered text is detected). Also handles user backspaces. 
     */
    function int readInt(String message) {
        var char key, c;
        var int length;
        var int num;
        var String str;

        let str = String.new(200);
        let str = Keyboard.readLine(message);
        let num = str.intValue();

        return num;
    }
}
